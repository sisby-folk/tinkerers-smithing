pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.19'
			def mc = mc_major + '.3'
			def fapi = "0.76.0"

			// // Build Tools // //
			version('loom', '1.2.+')

			plugin('loom', 'org.quiltmc.loom').versionRef('loom')

			// // Base // //
			version('mc', mc)
			version('ql', "0.19.0")
			version('qm', "${mc}+build.28")
			version('qsl', "4.0.0-beta.13+${mc}")
			version('fapi', "${fapi}-${mc}")
			version('qfapi', "5.0.0-beta.9+${fapi}-${mc}")

			library('mc', 'mojang', 'minecraft').versionRef('mc')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('qm', 'org.quiltmc', 'quilt-mappings').versionRef('qm')
			library('qsl', 'org.quiltmc', 'qsl').versionRef('qsl')
			library('qfapi', 'org.quiltmc.quilted-fabric-api', 'quilted-fabric-api',).versionRef('qfapi')

			// // Libraries // //
			version('emi', "1.0.4+${mc}")

			library('emi', 'dev.emi', 'emi-fabric').versionRef('emi')
		}
	}
}
