//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
	dependencies {
		classpath "com.modrinth.minotaur:Minotaur:${libs.versions.minotaur.get()}"
		classpath "com.github.breadmoirai:github-release:${libs.versions.githubRelease.get()}"
	}
}


plugins {
	id 'maven-publish'
	alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = project.name
group = 'folk.sisby'

repositories {
	mavenLocal()
	maven { url "https://maven.terraformersmc.com/" }
}

dependencies {
	minecraft libs.mc
	mappings variantOf(libs.yarn) { classifier "v2" }
	modImplementation libs.fl
	modImplementation libs.fapi
	implementation libs.kaleidoConfig

	modCompileOnly libs.emi

	modLocalRuntime libs.emi
	include libs.kaleidoConfig
}


processResources {
	final Map<String, String> map = [
		"version"      : version.toString(),
		"mc"           : libs.versions.mc.get(),
		"fl"           : libs.versions.fl.get(),
		"fapi"         : libs.versions.fapi.get(),
		"kaleidoConfig": libs.versions.kaleidoConfig.get(),
		"emi"            : libs.versions.emi.get()
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
}

loom {
	accessWidenerPath = file("src/main/resources/tinkerers_smithing.accesswidener")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

if (System.getenv("MODRINTH_TOKEN") && file("CHANGELOG.md").exists() ) {
	apply plugin: "com.modrinth.minotaur"
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'tinkerers-smithing'
		versionNumber = project.version
		versionName = baseVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [libs.versions.mc.get()]
		loaders = ['quilt', 'fabric']
		detectLoaders = false
		dependencies {
			required.version libs.fapi.get().getName(), libs.versions.fapi.get()
			optional.version libs.emi.get().getName().replace("-fabric", ""), libs.versions.emi.get() + "+fabric"
		}
		changelog = rootProject.file("CHANGELOG.md").text + "\n\nChangelog: https://github.com/sisby-folk/$project.name/releases/tag/v$baseVersion"
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN") && file("CHANGELOG.md").exists()) {
	apply plugin: "com.github.breadmoirai.github-release"
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "tinkerers-smithing"
		tagName 'v' + baseVersion
		releaseName baseVersion
		targetCommitish defaultBranch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
		body rootProject.file("CHANGELOG.md").text
	}
	tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
	tasks.register("fullRelease") {
		group "publishing"
		dependsOn "githubRelease"
		dependsOn "modrinth"
	}
}

